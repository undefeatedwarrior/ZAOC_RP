@Metadata.layer: #CUSTOMER
@UI.headerInfo: {
    typeName: 'Travel',
    typeNamePlural: 'Travels',
    title: { value: 'TravelId' },
    description: { value: '_Customer.FirstName'},
    typeImageUrl: 'https://encrypted-tbn0.gstatic.com/images?q=tbn:ANd9GcQ6ZYUaKnXuRQuF5r06axflMPVT7VQB_V9NHA&s'
    }

annotate entity ZATS_RP_TRAVEL_PROCESSOR with
{

  // Object Page Annotation
  @UI.facet: [
                {

                    purpose: #HEADER,
                    type: #DATAPOINT_REFERENCE,
                    label: 'Flight Ticket Price',
                    position: 10,
                    qualifier: 'price_data'

                 },
                {

                    purpose: #HEADER,
                    type: #DATAPOINT_REFERENCE,
                    label: 'Status',
                    position: 20,
                    qualifier: 'Stauts'

                 },
                 {

                    purpose: #STANDARD,
                    type: #COLLECTION, //Parent
                    position: 10,
                    id : 'super',
                    label: 'Additional Details'

                 },
                     {

                        purpose: #STANDARD,
                        type: #IDENTIFICATION_REFERENCE, // Child ( Only one Identifiaction Refence is possible)
                        label: 'More Info.',
                        position: 10,
                        parentId: 'super'

                     },
                     {

                        purpose: #STANDARD,
                        type: #FIELDGROUP_REFERENCE, // Child ( Multiple Fild Groups are allowed )
                        label: 'Price Data',
                        position: 20,
                        parentId: 'super',
                        targetQualifier: 'pricing'

                     },
                     {

                        purpose: #STANDARD,
                        type: #FIELDGROUP_REFERENCE, // Child ( Multiple Fild Groups are allowed )
                        label: 'Dates',
                        position: 30,
                        parentId: 'super',
                        targetQualifier: 'dates'

                     },
                     {

                        purpose: #STANDARD,
                        type: #FIELDGROUP_REFERENCE, // Child ( Multiple Fild Groups are allowed )
                        label: 'Admin Info.',
                        position: 40,
                        parentId: 'super',
                        targetQualifier: 'admin'

                     },
                 {

                    purpose: #STANDARD,
                    type: #LINEITEM_REFERENCE, //Parent
                    position: 20,
                    id : 'booking',
                    label: 'Bookings',
                    targetElement: '_Booking'

                 }


              ]

  @UI.selectionField: [{ position: 10 }]    // LR Selection
  @UI.lineItem: [{ position: 10 },
                 { type: #FOR_ACTION,
                   //inline: true ,
                   label: 'Copy Travel',
                   dataAction: 'copyTravel' } //Annotation for Copy Travel Option -- Data Action
  ]  // LR table
  // Object Page Annotation
  @UI.identification: [{ position: 10 } ,
                       { type: #FOR_ACTION,
                         label: 'Copy Travel',
                         dataAction: 'copyTravel' } //Annotation for Copy Travel Option -- Data Action
                   ]
  TravelId;

  @UI.selectionField: [{ position: 20 }]    // LR Selection
  @UI.lineItem: [{ position: 20 }]  // LR table
  // Object Page Annotation
  @UI.identification: [{ position: 20 }]
  AgencyId;

  @UI.selectionField: [{ position: 30 }]    // LR Selection
  @UI.lineItem: [{ position: 30 }]  // LR table
  // Object Page Annotation
  @UI.identification: [{ position: 30 }]
  CustomerId;

  @UI.lineItem: [{ position: 40 }] // LR table
  @UI.fieldGroup: [{ qualifier: 'dates' , label: 'Start Date' , position: 10 }]
  BeginDate;

  @UI.fieldGroup: [{ qualifier: 'dates' , label: 'End Date' , position: 20 }]
  EndDate;

  @UI.fieldGroup: [{ qualifier: 'pricing' , label: 'Booking Fee' , position: 10 }]
  BookingFee;

  @UI.selectionField: [{ position: 40 }]    // LR Selection
  @UI.lineItem: [{ position: 50 }]  // LR table
  // Object Page Annotation
  @UI.dataPoint:{ qualifier: 'price_data' ,
                  title: 'Flight Price' }             //Data Point for UI Facet
  @UI.fieldGroup: [{ qualifier: 'pricing' , label: 'Total Price' , position: 20 }]
  TotalPrice;

  //  @UI.lineItem: [{ position: 60 }]
  @UI.fieldGroup: [{ qualifier: 'pricing' , label: 'Currency Code' , position: 30 }]
  CurrencyCode;
  //  Description;

  @UI.selectionField: [{ position: 50 }]    // LR Selection
  @UI.lineItem: [{ position: 70 ,
                   criticality: 'Criticality' ,
                   importance: #HIGH }] // LR table
  // Object Page Annotation
  @UI.dataPoint:{ title: 'Travel Status' ,
                  qualifier: 'Status' ,
                  criticality: 'Criticality'
                }
  @UI.fieldGroup: [{ qualifier: 'dates' , label: 'Status' , position: 30 }]
  overallstatus;

  //  StatusText;
  //  Criticality;

  @UI.fieldGroup: [{ qualifier: 'admin' , label: 'Created By' , position: 10 }]
  CreatedBy;

  @UI.fieldGroup: [{ qualifier: 'admin' , label: 'Created On' , position: 20 }]
  CreatedAt;

  @UI.fieldGroup: [{ qualifier: 'admin' , label: 'Changed By' , position: 30 }]
  LastChangedBy;

  @UI.fieldGroup: [{ qualifier: 'admin' , label: 'Changed On' , position: 40 }]
  LastChangedAt;
}