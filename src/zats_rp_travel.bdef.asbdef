managed implementation in class zbp_ats_rp_travel unique;
strict ( 2 );

//Add draft feature at BO level
with draft;

//Root Entity
define behavior for ZATS_RP_TRAVEL alias Travle

// Make behavour pool for travel entity
implementation in class zbp_ats_rp_travel unique

persistent table /dmo/travel_m
lock master
total etag LastChangedAt    // Mandatory to use total etag with lock master when Draft is used
authorization master ( instance )

//Draft Table for Travel - Will be created by RAP Framework
//Positioning of Draft table is must be after Lock Master and Authorization Master
draft table zats_rp_d_travel

etag master LastChangedAt

with additional save
//Early Numbering

early numbering
{
  create ( authorization : global );
  update;
  delete;

  //Dynamic Feature Control
  association _Booking { create ( features : instance ); with draft; }


  //It is piece of code which is intented to be only consumed with in our RAP BO
  internal action reCalculateTotalPrice;


  determination calculateTotalPrice on modify
  {
    create; //On creation of new booking
    field BookingFee, CurrencyCode; //on change of field
  }

  //Validation - Check custom business rules
  validation validateHeaderData on save
  { create;
    field CustomerId, BeginDate, EndDate;
  }


  //Static Feature control
  field ( readonly ) TravelId, CreatedBy, CreatedAt, LastChangedAt, LastChangedBy, TotalPrice;
  field ( mandatory ) AgencyId, CustomerId, BeginDate, EndDate, overallstatus, BookingFee, CurrencyCode;


  //Data action to copy BO data
  factory action copyTravel [1]; //Data Action - Factory Data (Instance)

  determine action validationCustomer { validation validateHeaderData; }
  //Side Effect
  side effects
  {
    field BookingFee affects field TotalPrice;
    determine action validationCustomer executed on field CustomerId affects messages;
  }

  //Explicit Draft Actions Are Needed in Strinct mode,
  //becuase RAP will consider actions declaed explicitly in BDEF  rather than relying on implicit framework defaults
  draft determine action Prepare;
  draft action Edit;
  draft action Resume;
  draft action Activate;
  draft action Discard;


  mapping for /dmo/travel_m corresponding
    {
      TravelId      = travel_id; //key
      AgencyId      = agency_id;
      CustomerId    = customer_id;
      BeginDate     = begin_date;
      EndDate       = end_date;
      TotalPrice    = total_price;
      BookingFee    = booking_fee;
      CurrencyCode  = currency_code;
      Description   = description;
      OverallStatus = overall_status;
      CreatedBy     = created_by;
      LastChangedBy = last_changed_by;
      CreatedAt     = created_at;
      LastChangedAt = last_changed_at;
    }
}



//Child for Travel
define behavior for ZATS_RP_BOOKING alias Booking

// Make behavour pool for booking entity
implementation in class zbp_ats_rp_booking unique

persistent table /dmo/booking_m
lock dependent by _Travel
authorization dependent by _Travel
etag master LastChangedAt

//Draft Table for Booking
draft table zats_rp_d_book

//Early Numbering
early numbering
{
  update;
  delete;

  //Static Feature control
  field ( readonly ) TravelId, BookingId, LastChangedAt;
  field ( mandatory ) CarrierId, ConnectionId, FlightDate, BookingStatus;

  association _Travel { with draft; }
  association _BookingSupp { create; with draft; }


  //determination for change in booking data
  determination calculateTotalPrice on modify
  {
    create;
    field FlightPrice, CurrencyCode; //on change of field
  }


  mapping for /dmo/booking_m
    {
      TravelId      = travel_id; //key
      BookingId     = booking_id; //key
      BookingDate   = booking_date;
      CustomerId    = customer_id;
      CarrierId     = carrier_id;
      ConnectionId  = connection_id;
      FlightDate    = flight_date;
      FlightPrice   = flight_price;
      CurrencyCode  = currency_code;
      BookingStatus = booking_status;
      LastChangedAt = last_changed_at;
    }
}


//Child for Booking
define behavior for ZATS_RP_BOOKSUPPL alias BookSuppl

// Make behavour pool for booking suppliment entity
implementation in class zbp_ats_rp_BookSuppl unique


persistent table /dmo/booksuppl_m
lock dependent by _Travel
authorization dependent by _Travel
etag master LastChangedAt

draft table zats_rp_d_booksp
early numbering
{
  update;
  delete;

  //Static Feature control
  field ( readonly ) TravelId, BookingId, BookingSupplementId, LastChangedAt;
  field ( mandatory ) Price, SupplementId;

  association _Travel { with draft; }
  association _Booking { with draft; }

  mapping for /dmo/booksuppl_m
    {
      TravelId            = travel_id; //key
      BookingId           = booking_id; //key
      BookingSupplementId = booking_supplement_id; //key
      SupplementId        = supplement_id;
      Price               = price;
      CurrencyCode        = currency_code;
      LastChangedAt       = last_changed_at;
    }
}